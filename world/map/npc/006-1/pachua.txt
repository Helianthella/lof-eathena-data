006-1,23,100,0|script|Pachua|143,
{
    set @LEATHER_PATCH_PRICE, 300;
// @wants_leather_patch is used as boolean
    set @wants_leather_patch, QUEST_Forestbow_state & NIBBLE_4_MASK;

    if ((gettime(5) >= $@xmas2011_start_day) && (gettime(5) < $@xmas2011_reward_start_day) && (gettime(6) == 12) && (gettime(7) == $@xmas2011_year))
        set @wants_leather_patch, @wants_leather_patch | (xmas11 & $@xmas11_talkedToChief);

    set $@xmas2011_start_day, 10;
    set $@xmas2011_reward_start_day, 25;

// lines belong to easter 2011:
//    set @month, 4;
//    set @start_day, 15;
//    set @end_day, 30;

    if (QUEST_MIRIAM_cheat != 0) goto L_warp_cheat;
    if (QUEST_MIRIAM_start != 0) goto L_smoke;

// lines belong to easter 2011:
//    if (gettime(6) == @month && gettime(5) >= @start_day && gettime(5) <= @end_day && QUEST_Easter11 == 1) goto L_Basket;
//    if (gettime(6) == @month && gettime(5) >= @start_day && gettime(5) <= @end_day && QUEST_Easter11 == 2) goto L_MakeBasket;
//    if (gettime(6) == @month && gettime(5) >= @start_day && gettime(5) <= @end_day && QUEST_Easter11 == 3) goto L_SeeBunny;

    set @hw2011_npc_id, $@hw2011_npc_pachua;
    if (gettime(7) == $@hw2011_year && gettime(6) == 10 && gettime(5) >= $@hw2011_start_day)
        goto L_TrickOrTreat;

    goto L_Begin;

L_Begin:
    mes "[Chief Pachua]";
    mes "\"How!\"";
    next;

    if (getequipid(equip_head) == 643 || getequipid(equip_head) == 644) goto L_WearingCowboy;
    if (getequipid(equip_legs) == 642) goto L_WearingChaps;

    mes "[Chief Pachua]";
    mes "\"For generations my tribe has been crafting special clothes out of different items.\"";
    next;
    goto L_Check_Mamba;

L_Check_Mamba:
    if (countitem("BlackMambaSkin") == 0 || QUEST_Mamba > 5 || BaseLevel < 50) goto L_Check_Shops;
    if (QUEST_Mamba > 1) goto L_Check_Mamba_Again;

    mes "[Chief Pachua]";
    mes "\"Ah! Do I see a Black Mamba Skin sticking out of your bag?\"";
    menu
        "Yes, can you do something with it?", L_Branch_Mamba,
        "Yes, but that's not why I'm here to see you.", L_Check_Shops;

L_Check_Mamba_Again:
    mes "[Chief Pachua]";
    mes "\"I see you have a Black Mamba Skin with you, are you ready for me to craft something with it?\"";
    menu
        "Yes, take a look.", L_Branch_Mamba,
        "No, that's not why I'm here to see you.", L_Check_Shops;

L_Branch_Mamba:
    if (QUEST_Mamba == 5) goto L_Mamba_Helm;
    if (QUEST_Mamba == 4) goto L_Mamba_Chest;
    if (QUEST_Mamba == 3) goto L_Mamba_Legs;
    if (QUEST_Mamba == 2) goto L_Mamba_Boots;
    if (QUEST_Mamba == 1) goto L_Mamba_Gloves;

    mes "[Chief Pachua]";
    mes "\"The skin of the Black Mamba can be used to craft a leather armor that is lighter than any metal armor and stronger than any other leather.";
    mes "\"Because it is leather, it can be used to make every type of armor.";
    mes "\"Although I know of no-one with the expertise to craft a shield, I can make the other pieces for you.";
    mes "\"I'll start with the gloves, but I'll need two Black Mamba Skins, one Black Mamba Tongue for thread, two Black Mamba Eggs to soften the leather, a Ruby, and 5,000 gold.\"";

    set QUEST_Mamba, 1;

    menu
        "I actually have all of that already!", L_Mamba_Gloves_Check,
        "Maybe later....", L_Close;

L_Mamba_Gloves:
    mes "[Chief Pachua]";
    mes "\"For a pair of Black Mamba Gloves I'll need two Black Mamba Skins, one Black Mamba Tongue for thread, two Black Mamba Eggs to soften the leather, a Ruby, and 5,000 gold.\"";
    menu
        "Here you are.", L_Mamba_Gloves_Check,
        "I'll think about it.", L_Close;

L_Mamba_Gloves_Check:
    getinventorylist;
    if (@inventorylist_count == 100) goto L_TooMany;
    if (countitem("BlackMambaSkin") < 2 || countitem("BlackMambaTongue") < 1 ||
        countitem("BlackMambaEgg") < 2 || countitem("Ruby") < 1) goto L_MissingItems;
    if (Zeny < 5000) goto L_NoMoney;

    delitem "BlackMambaSkin", 2;
    delitem "BlackMambaTongue", 1;
    delitem "BlackMambaEgg", 2;
    delitem "Ruby", 1;
    set Zeny, Zeny - 5000;

    getitem "BlackMambaGloves", 1;
    set QUEST_Mamba, 2;
    goto L_DealDone;

L_Mamba_Boots:
    mes "[Chief Pachua]";
    mes "\"For a pair of Black Mamba Boots I'll need four Black Mamba Skins, two Black Mamba Tongues for thread, two Rubies, and 10,000 gold.\"";
    menu
        "I'll take them.", L_Mamba_Boots_Check,
        "That's way too much!", L_Close;

L_Mamba_Boots_Check:
    getinventorylist;
    if (@inventorylist_count == 100) goto L_TooMany;
    if (countitem("BlackMambaSkin") < 4 || countitem("BlackMambaTongue") < 2 ||
        countitem("Ruby") < 2) goto L_MissingItems;
    if (Zeny < 10000) goto L_NoMoney;

    delitem "BlackMambaSkin", 4;
    delitem "BlackMambaTongue", 2;
    delitem "Ruby", 2;
    set Zeny, Zeny - 10000;

    getitem "BlackMambaBoots", 1;
    set QUEST_Mamba, 3;
    goto L_DealDone;

L_Mamba_Legs:
    mes "[Chief Pachua]";
    mes "\"For a pair of Black Mamba Pants I'll need six Black Mamba Skins, three Black Mamba Tongues for thread, six Black Mamba Eggs to soften the leather, three Rubies, and 20,000 gold.\"";
    menu
        "Here you go!", L_Mamba_Legs_Check,
        "Um, no thanks.", L_Close;

L_Mamba_Legs_Check:
    getinventorylist;
    if (@inventorylist_count == 100) goto L_TooMany;
    if (countitem("BlackMambaSkin") < 6 || countitem("BlackMambaTongue") < 3 ||
        countitem("BlackMambaEgg") < 6 || countitem("Ruby") < 3) goto L_MissingItems;
    if (Zeny < 20000) goto L_NoMoney;

    delitem "BlackMambaSkin", 6;
    delitem "BlackMambaTongue", 3;
    delitem "BlackMambaEgg", 6;
    delitem "Ruby", 3;
    set Zeny, Zeny - 20000;

    getitem "BlackMambaPants", 1;
    set QUEST_Mamba, 4;
    goto L_DealDone;

L_Mamba_Chest:
    mes "[Chief Pachua]";
    mes "\"For a Black Mamba Armor I'll need ten Black Mamba Skins, ten Black Mamba Tongues for stitching, two Iron Ingots for strength, ten Rubies, and 250,000 gold.\"";
    menu
        "I'll take it.", L_Mamba_Chest_Check,
        "That's outrageous!", L_Close;

L_Mamba_Chest_Check:
    getinventorylist;
    if (@inventorylist_count == 100) goto L_TooMany;
    if (countitem("BlackMambaSkin") < 10 || countitem("BlackMambaTongue") < 10 ||
        countitem("IronIngot") < 2 || countitem("Ruby") < 10) goto L_MissingItems;
    if (Zeny < 250000) goto L_NoMoney;

    delitem "BlackMambaSkin", 10;
    delitem "BlackMambaTongue", 10;
    delitem "IronIngot", 2;
    delitem "Ruby", 10;
    set Zeny, Zeny - 250000;

    getitem "BlackMambaArmor", 1;
    set QUEST_Mamba, 5;
    goto L_DealDone;

L_Mamba_Helm:
    mes "[Chief Pachua]";
    mes "\"For a Black Mamba Helm I'll need eight Black Mamba Skins, eight Black Mamba Tongues for stitching, eight Black Mamba Eggs to soften the interior, twenty Rubies to absorb blows, and 100,000 gold.\"";
    menu
        "Can I get that boxed?", L_Mamba_Helm_Check,
        "Not worth it to me right now.", L_Close;

L_Mamba_Helm_Check:
    if (countitem("BlackMambaSkin") < 8 || countitem("BlackMambaTongue") < 8 ||
        countitem("BlackMambaEgg") < 8 || countitem("Ruby") < 20) goto L_MissingItems;
    if (Zeny < 100000) goto L_NoMoney;

    delitem "BlackMambaSkin", 8;
    delitem "BlackMambaTongue", 8;
    delitem "BlackMambaEgg", 8;
    delitem "Ruby", 20;
    set Zeny, Zeny - 100000;

    getitem "BlackMambaHelm", 1;
    set QUEST_Mamba, 6;
    goto L_DealDone;

L_Check_Shops:
    if ((countitem("JeansShorts") >= 1 && countitem("SnakeSkin") >= 10) && (countitem("FancyHat") >= 1 && countitem("SnakeSkin") >= 2)) goto L_Super_store;
    if (countitem("JeansShorts") >= 1 && countitem("SnakeSkin") >= 10) goto L_Chaps_store;
    if (countitem("FancyHat") >= 1 && countitem("SnakeSkin") >= 2) goto L_Cowboy_store;

    mes "[Chief Pachua]";
    mes "\"Maybe if you bring me the right materials I can make something for you.\"";
    next;
    if (@wants_leather_patch)
        menu
            "Wait, can you make a leather patch for me?", L_leather_patch,
            "OK, bye.", L_Close;
    goto L_Super_store;

S_CheckStuff:
    mes "[Chief Pachua]";
    mes "\"Let me see what you have there.\"";
    next;
    return;

L_Super_store:
    callsub S_CheckStuff;
    mes "[Chief Pachua]";
    mes "\"Ahh you have lots of good items to work with.\"";
    mes "\"With them I can make you either a Cowboy hat or Snake Skin Chaps\"";
    next;
    if (@wants_leather_patch)
        menu
            "Cowboy hat, please.", L_BuyCowboy,
            "Snake Skin Chaps sound good.", L_BuyChaps,
            "Can you make a leather patch?", L_leather_patch,
            "Not now, maybe later.", L_NoDeal;
    menu
        "Cowboy hat, please.", L_BuyCowboy,
        "Snake Skin Chaps sound good.", L_BuyChaps,
        "Not now, maybe later.", L_NoDeal;

L_Cowboy_store:
    callsub S_CheckStuff;
    mes "[Chief Pachua]";
    mes "\"To make you a Cowboy hat I will need:";
    mes "1 Fancy hat";
    mes "2 Snake skins";
    mes "5.000 GP\"";
    mes "";
    mes "\"Do we have a deal?\"";
    next;
    if (@wants_leather_patch)
        menu
            "Yes, that's fine.", L_BuyCowboy,
            "Can you make a leather patch?", L_leather_patch,
            "On second thought, maybe later.", L_NoDeal;
    menu
        "Yes, that's fine.", L_BuyCowboy,
        "On second thought, maybe later.", L_NoDeal;

L_Chaps_store:
    callsub S_CheckStuff;
    mes "[Chief Pachua]";
    mes "\"To make you a pair of Snake Skin Chaps I will need:";
    mes "1 Jeans Shorts";
    mes "10 Snake skins";
    mes "10.000 GP\"";
    mes "";
    mes "\"Do we have a deal?\"";
    if (!@wants_leather_patch)
        menu
            "Yes, that's fine.", L_BuyChaps,
            "On second thought, maybe later.", L_NoDeal;
    if (@wants_leather_patch)
        menu
            "Yes, that's fine.", L_BuyChaps,
            "Can you make a leather patch?", L_leather_patch,
            "On second thought, maybe later.", L_NoDeal;

L_BuyChaps:
    getinventorylist;
    if (@inventorylist_count == 100) goto L_TooMany;
    if (Zeny < 10000) goto L_NoMoney;
    if (countitem("JeansShorts") < 1) goto L_NoJeans;
    if (countitem("SnakeSkin") < 10) goto L_NoSkins;
    set Zeny, Zeny - 10000;
    delitem "SnakeSkin", 10;
    delitem "JeansShorts", 1;
    getitem "JeansChaps", 1;
    goto L_DealDone;

L_BuyCowboy:
    getinventorylist;
    if (@inventorylist_count == 100) goto L_TooMany;
    if (Zeny < 5000) goto L_NoMoney;
    if (countitem("FancyHat") < 1) goto L_NoFancy;
    if (countitem("SnakeSkin") < 2) goto L_NoSkins;
    set Zeny, Zeny - 5000;
    delitem "SnakeSkin", 2;
    delitem "FancyHat", 1;
    set @temp,rand(2);
    if(@temp == 0) goto L_Cowboy_white;
    goto L_Cowboy_black;

L_Cowboy_white:
    getitem "WhiteCowboyHat", 1;
    goto L_DealDone;

L_Cowboy_black:
    getitem "BlackCowboyHat", 1;
    goto L_DealDone;

L_leather_patch:
    mes "[Chief Pachua]";
    mes "\"If you just want a piece of leather, then yes, I can make that.  Bring me a snake skin and " + @LEATHER_PATCH_PRICE + " GP.\"";
    next;
    menu
        "Here you are.", L_CheckPatchItems,
        "OK, I'll be back later.", L_Close,
        "That's too expensive!.", L_NoDeal;

L_CheckPatchItems:
    if (countitem("SnakeSkin") < 1) goto L_NoSkins;
    if (Zeny < @LEATHER_PATCH_PRICE) goto L_NoMoney;
    getinventorylist;
    if (@inventorylist_count == 100 && countitem("SnakeSkin") > 1) goto L_TooMany;

    set Zeny, Zeny - @LEATHER_PATCH_PRICE;
    delitem "SnakeSkin", 1;
    getitem "LeatherPatch", 1;
    goto L_DealDone;

L_DealDone:
    mes "[Chief Pachua]";
    mes "\"Here you are!";
    mes "Come back any time.\"";
    goto L_Close;

L_NoDeal:
    mes "[Chief Pachua]";
    mes "\"Alright, but you won't get a better deal anywhere else!\"";
    goto L_Close;

L_MissingItems:
    mes "[Chief Pachua]";
    mes "\"You don't seem to have all the items I need to craft that. Come back when you do.\"";
    goto L_Close;

L_NoMoney:
    mes "[Chief Pachua]";
    mes "\"Oh dear, it seems you don't have enough money.\"";
    goto L_Close;

L_NoJeans:
    mes "[Chief Pachua]";
    mes "\"Oh dear, it seems you don't have enough jean shorts.\"";
    goto L_Close;

L_NoFancy:
    mes "[Chief Pachua]";
    mes "\"Oh dear, it seems you don't have enough fancy hats.\"";
    goto L_Close;

L_NoSkins:
    mes "[Chief Pachua]";
    mes "\"Oh dear, it seems you don't have enough snake skins.\"";
    goto L_Close;

L_WearingCowboy:
    mes "[Chief Pachua]";
    mes "\"Ah, I see that you are wearing a hat made with the ancient methods of my tribe.\"";
    next;
    goto L_Check_Shops;

L_WearingChaps:
    mes "[Chief Pachua]";
    mes "\"Ah, I see that you are wearing pants made by my tribe.\"";
    next;
    goto L_Check_Shops;

L_Close:
    set @LEATHER_PATCH_PRICE, 0;
    set @wants_leather_patch, 0;
    set @month, 0;
    set @start_day, 0;
    set @end_day, 0;
    set @temp, 0;
    close;

L_TooMany:
    mes "[Chief Pachua]";
    mes "\"You don't have room for another item. Come back later.\"";
    goto L_Close;

L_smoke:
    message strcharinfo(0), "Pachua quickly inhales from his pipe and releases a ring of smoke towards the sky!";
    set QUEST_MIRIAM_run, gettimetick(2) - QUEST_MIRIAM_start;
    set QUEST_MIRIAM_start, 0;
    close;

L_warp_cheat:
    if (@warp_cheat == 1) goto L_Begin;
    message strcharinfo(0), "Pachua releases a ring of smoke towards the sky! But, by the look on his face, you can tell he is suspicious about your methods...";
    set @warp_cheat, 1;
    end;

L_Basket:
    mes "[Chief Pachua]";
    mes "\"For generations my tribe has been crafting special clothes out of different items.\"";
    mes "\"Maybe if you bring me the right materials I can make something for you.\"";
    next;
    menu
        "Do you work only with clothes? Because I was looking for a basket.", L_AskBasket;

L_AskBasket:
    mes "\"A basket? In our tribe, we craft baskets of all kinds using only reeds. This basket, you need it to carry your items?\"";
    menu
        "No. It is for the Easter Bunny. I offered to get him one.", L_EasterBunny;

L_EasterBunny:
    mes "\"I appreciate your attitude. Maybe I can help you with that.\"";
    menu
        "I would be really grateful if you could do that!", L_SetStatus,
        "Nah, I decided not to do that stupid quest.", L_Close,
        "I would prefer to talk to you about other stuff.", L_Begin; //should go to pachuas's regular chat

L_SetStatus:
    set QUEST_Easter11, 2;

L_MakeBasket:
    mes "[Chief Pachua]";
    mes "\"I will need you to gather reeds for me.\"";
    mes "\"5 bundles should do it.\"";
    mes "\"You should be able to get those from mouboos as they graze.\"";
    menu
        "I have the Reeds!", L_CheckBasketItems,
        "Ok, I will be back soon", L_Close,
        "I changed my mind, forget about it", L_Close,
        "Can I talk to you about another stuff?", L_Begin; //should go to pachuas's regular chat

L_CheckBasketItems:
    if(countitem("ReedBundle") < 5) goto L_EasterNotEnough;
    if(countitem("ReedBundle") >= 5) goto L_EasterEnough;
    goto L_Close;

L_EasterNotEnough:
    mes "[Chief Pachua]";
    mes "\"You do not have enough Reed Bundles for me to work with.\"";
    mes "\"Go gather more.\"";
    goto L_Close;

L_EasterEnough:
    getinventorylist;
    if (@inventorylist_count == 100 && countitem("ReedBundle") > 5) goto L_EasterTooMany;
    if(countitem("ReedBundle") < 5) goto L_EasterNotEnough;
    delitem "ReedBundle", 5;
    getitem "EasterBasket", 1;
    set QUEST_Easter11, 3;
    mes "[Chief Pachua]";
    mes "\"You have gathered enough reeds for me to make the basket.\"";
    mes "He skillfully soaks the reeds you brought him,";
    mes "then swiftly weaves them into a basket shape.";
    mes "Next, he places the damp basket in the sun to dry for a minute before handing it over to you.";
    mes "\"Here is your Easter Basket.\"";
    goto L_Close;

L_EasterTooMany:
    mes "[Chief Pachua]";
    mes "\"You don't have room for the Easter Basket. Come back later.\"";
    goto L_Close;

L_SeeBunny:
    mes "[Chief Pachua]";
    mes "\"You need to return to the Easter Bunny now.\"";
    next;
    goto L_Begin;

L_TrickOrTreat:
    callfunc "TrickOrTreat2011";
    goto L_Begin;
}
