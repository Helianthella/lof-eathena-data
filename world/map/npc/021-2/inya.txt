//Gemstones: Diamond (white) , Ruby (red), Emerald (green), Sapphire (blue), Topaz (yellow), Amethyst (purple)
//The power of the gems is important to future balance of these rings.  Are they related to stats, tiers of power, schools of magic?

021-2,80,134,0|script|Inya|106
{
    mes "[Inya]";
    mes "\"I am Inya, a jeweler of fine rings.  If you have a Simple Ring and a gem, I can make a nice ring for you for 100,000 gold pieces.\"";
    next;
    mes "\"Also, if you have a ring with gems on it, I can remove the gems and return the Simple Ring to you for 5,000 gold pieces.\"";
    next;
    mes "\"So, how can I help you?\"";
    menu
        "I have a wedding ring, will that do?", L_Inya_Wedding_Ring,
        "I'd like to remove a gem (5,000 GP).", L_Inya_Remove_RingGem,
        "I'd like to add a gem (100,000 GP).", L_Inya_Add_RingGem,
        "Not interested.", L_Inya_NotInterested;

L_Inya_Wedding_Ring:
    mes "[Inya]";
    mes "\"No, sorry.  I do not alter wedding rings at this time.\"";
    close;

L_Inya_Remove_RingGem:
    mes "[Inya]";
    mes "\"What kind of gem would you like to have removed?\"";
    menu
        "Diamond.", L_Inya_Remove_Diamond,
        "Ruby.", L_Inya_Remove_Ruby,
        "Emerald.", L_Inya_Remove_Emerald,
        "Sapphire.", L_Inya_Remove_Sapphire,
        "Topaz.", L_Inya_Remove_Topaz,
        "Amethyst.", L_Inya_Remove_Amethyst,
        "I've changed my mind.", L_Inya_NotInterested;

L_Inya_Add_RingGem:
    mes "[Inya]";
    mes "\"What kind of gem would you like to add?\"";
    menu
        "Diamond.", L_Inya_Add_Diamond,
        "Ruby.", L_Inya_Add_Ruby,
        "Emerald.", L_Inya_Add_Emerald,
        "Sapphire.", L_Inya_Add_Sapphire,
        "Topaz.", L_Inya_Add_Topaz,
        "Amethyst.", L_Inya_Add_Amethyst,
        "I've changed my mind.", L_Inya_NotInterested;

L_Inya_Remove_Diamond:
    set @gem_type$, "Diamond";
    goto L_Remove;

L_Inya_Remove_Ruby:
    set @gem_type$, "Ruby";
    goto L_Remove;

L_Inya_Remove_Emerald:
    set @gem_type$, "Emerald";
    goto L_Remove;

L_Inya_Remove_Sapphire:
    set @gem_type$, "Sapphire";
    goto L_Remove;

L_Inya_Remove_Topaz:
    set @gem_type$, "Topaz";
    goto L_Remove;

L_Inya_Remove_Amethyst:
    set @gem_type$, "Amethyst";
    goto L_Remove;

L_Remove:
    if (Zeny < 5000 || countitem(@gem_type$ + "Ring") < 1) goto L_Inya_Not_Enough;
    set Zeny, Zeny - 5000;
    delitem @gem_type$ + "Ring", 1;
    getitem "SimpleRing", 1;
    getitem @gem_type$ + "Powder", 2 + rand(3);
    mes "[Inya]";
    mes "\"Here you go.\"";
    close;


L_Inya_Add_Diamond:
    set @gem_type$, "Diamond";
    goto L_Add;

L_Inya_Add_Ruby:
    set @gem_type$, "Ruby";
    goto L_Add;

L_Inya_Add_Emerald:
    set @gem_type$, "Emerald";
    goto L_Add;

L_Inya_Add_Sapphire:
    set @gem_type$, "Sapphire";
    goto L_Add;

L_Inya_Add_Topaz:
    set @gem_type$, "Topaz";
    goto L_Add;

L_Inya_Add_Amethyst:
    set @gem_type$, "Amethyst";
    goto L_Add;

L_Add:
    if (Zeny < 100000 || countitem("SimpleRing") < 1 || countitem(@gem_type$) < 1)
        goto L_Inya_Not_Enough;

    set Zeny, Zeny - 100000;
    delitem "SimpleRing", 1;
    delitem @gem_type$, 1;
    getitem @gem_type$ + "Ring", 1;
    mes "[Inya]";
    mes "\"Here you go.\"";
    close;


L_Inya_NotInterested:
    mes "[Inya]";
    mes "\"Come back later and I will try to be of assistance.\"";
    close;

L_Inya_Not_Enough:
    mes "[Inya]";
    mes "\"I am sorry, you simply do not seem to have the necessary items for me to help you.  Please come back when you do, and I would be more than happy to help you out.\"";
    close;
}
